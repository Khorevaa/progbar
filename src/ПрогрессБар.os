///////////////////////////////////////////////////////////////////////////////
// Класс реализации прогресс бара консольных приложений
//
// (C) Maximov Valеry aka TheShadowCo
///////////////////////////////////////////////////////////////////////////////

Перем Консоль;
Перем ТекстПодсказки;
Перем НадоВыводитьПроценты;
Перем НадоВыводитьНомерИКоличество;
Перем КоличествоШаговПрогресса;
Перем ЦветТекстаКонсоли;

Перем ПоследнийШаг;

Перем Инициализирован;

Перем ПоложениеКурсораВерх;
Перем ПоложениеКурсораНиз;

// Начать
//	Выполняет инициализацию прогресс бара
//
// Параметры:
//  КоличествоШагов 			- Число 	- Общее количество шагов прогресса
//	Подсказка 					- Строка 	- Текст, выводимый перед строкой прогресса
//	ВыводитьПроценты 			- Булево 	- Флаг отображения прогресса в процентах (в конце строки)
//	ВыводитьНомерИКоличество 	- Булево 	- Флаг вывода прогресса в абсолютном виде
//
Процедура Начать(КоличествоШагов, Подсказка = "", ВыводитьПроценты = ИСТИНА, ВыводитьНомерИКоличество = ИСТИНА) Экспорт

	Консоль = Новый Консоль;
	ЦветТекстаКонсоли = Консоль.ЦветТекста;
	ТекстПодсказки = Подсказка;
	НадоВыводитьПроценты = ВыводитьПроценты;
	НадоВыводитьНомерИКоличество = ВыводитьНомерИКоличество;
	КоличествоШаговПрогресса = Число(КоличествоШагов);
	Если КоличествоШаговПрогресса < 0 Тогда

		ВызватьИсключение "Количество шагов должно быть больше 0";

	КонецЕсли;

	ПоследнийШаг = 0;
	Инициализирован = ИСТИНА;

	ПоложениеКурсораВерх = 0;
	ПоложениеКурсораНиз = 0;

КонецПроцедуры // Начать

// СделатьШаг
//	Двигает строку прогресса на шаг в перед. Предоставляется возможность управлять
//	размером шага, общим количеством и подсказкой
//
// Параметры:
//	Инкремент 			- Число 	- Размер и направление (для отрицательных значений) шага
//	Подсказка 			- Строка 	- Текст, выводимый перед строкой прогресса
//  КоличествоШагов 	- Число 	- Общее количество шагов прогресса
//
Процедура СделатьШаг(Инкремент = 1, КоличествоШагов = 0, Подсказка = Неопределено) Экспорт
	
	Если Не Инициализирован Тогда

		ВызватьИсключение "Прогресс бар не инициализирован. Воспользуйтесь методом Начать";

	КонецЕсли;

	Если КоличествоШагов > 0 Тогда

		КоличествоШаговПрогресса = КоличествоШагов;

	КонецЕсли;

	ПоследнийШаг = ПоследнийШаг + Инкремент;
	Если ПоследнийШаг < 0 Тогда

		ПоследнийШаг = 0;

	ИначеЕсли ПоследнийШаг > КоличествоШаговПрогресса Тогда

		ПоследнийШаг = КоличествоШаговПрогресса;

	КонецЕсли;
	
	Если Подсказка <> Неопределено Тогда

		ТекстПодсказки = Подсказка;

	КонецЕсли;

	Консоль.ВидимостьКурсора(Ложь);	
	ПоложениеКурсораНиз = Консоль.КурсорВерх;

	Если ПоложениеКурсораВерх <> 0 Тогда
		Консоль.КурсорВерх = ПоложениеКурсораВерх - 1; // наш перевод строки
	КонецЕсли;

	Если ЗначениеЗаполнено(ТекстПодсказки) Тогда

		Консоль.ЦветТекста = ЦветКонсоли.Green;
		Консоль.Вывести(ТекстПодсказки);

	КонецЕсли;

	Если НадоВыводитьНомерИКоличество Тогда

		Консоль.ЦветТекста = ЦветКонсоли.Yellow;
		Консоль.Вывести(СтрШаблон("(%1 из %2) ", ПоследнийШаг, КоличествоШаговПрогресса));

	КонецЕсли;

	Консоль.ЦветТекста = ЦветКонсоли.Magenta;
	
	Позиция = Цел(ПоследнийШаг / КоличествоШаговПрогресса * 100 / 2);

	Значек = Сред("/-\|", ПоследнийШаг % 4 + 1, 1);
	СтрокаЗаполненногоПрогресса = Сред("--------------------------------------------------", 1, Позиция);
	СтрокаНеЗаполненногоПрогресса = Сред("                                                  ", Позиция);

	Консоль.Вывести(СтрШаблон("%1 |%2%3| ", Значек, СтрокаЗаполненногоПрогресса, СтрокаНеЗаполненногоПрогресса));

	Если НадоВыводитьПроценты Тогда
		
		Консоль.ЦветТекста = ЦветКонсоли.Yellow;
		Консоль.Вывести("" + Цел(ПоследнийШаг / КоличествоШаговПрогресса * 100) + "% ");

	КонецЕсли;
	
	Консоль.КурсорЛево = 0;
	Консоль.Вывести(Символы.ПС);
	Консоль.ЦветТекста = ЦветТекстаКонсоли;

	Если ПоложениеКурсораВерх = 0 Тогда
		
		ПоложениеКурсораВерх = Мин(Консоль.КурсорВерх, Консоль.Высота);

	КонецЕсли;

	ПоложениеКурсораНиз = Мин(ПоложениеКурсораНиз, Консоль.Высота);

	Если ПоложениеКурсораНиз > Консоль.КурсорВерх Тогда

		Консоль.КурсорВерх = ПоложениеКурсораНиз;
		
	КонецЕсли;

КонецПроцедуры // СделатьШаг

// Завершить
//	Завершает вывод строки прогресс бара
//
Процедура Завершить() Экспорт

	Инициализирован = ЛОЖЬ;
	Консоль.ВидимостьКурсора(Истина);
	Консоль.ЦветТекста = ЦветТекстаКонсоли;
	Консоль.ВывестиСтроку("");
	Консоль = Неопределено;
	
КонецПроцедуры // Завершить

///////////////////////////////////////////////////////////////////////////////

Инициализирован = ЛОЖЬ;
